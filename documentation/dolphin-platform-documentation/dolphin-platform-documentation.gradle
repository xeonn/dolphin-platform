/*
 * Copyright 2012-2016 Canoo Engineering AG.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'org.ajoberstar:gradle-git:1.3.2'
    }
}

apply plugin: 'org.asciidoctor.convert'
apply plugin: 'org.ajoberstar.github-pages'

repositories {
    jcenter()
}

asciidoctorj {
    version = '1.5.4'
}

asciidoctor {
    options doctype: 'book'

    backends = ['html5']

    attributes = [
        toc                    : 'left',
        doctype                : 'book',
        icons                  : 'font',
        encoding               : 'utf-8',
        sectlink               : true,
        sectanchors            : true,
        numbered               : true,
        linkattrs              : true,
        imagesdir              : 'images',
        linkcss                : true,
        'source-highlighter'   : 'coderay',
        'coderay-linenums-mode': 'table',
        'revision-number'      : project.version
    ]

    sources {
        include 'index.adoc'
    }

    resources {
        from(file('src/docs/asciidoc/images')) {
            into 'images'
        }
    }
}

if (!project.hasProperty('githubUsername')) ext.githubUsername = ''
if (!project.hasProperty('githubPassword')) ext.githubPassword = ''

githubPages {
    repoUri = project.project_scm
    pages {
        from asciidoctor.outputs.files
    }

    credentials {
        username = githubUsername
        password = githubPassword
    }
}

publishGhPages.dependsOn(asciidoctor)